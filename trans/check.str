module check

imports
  include/MoBL
  lookup
  type
  rename
  desugar
  mobl
  pp
  generation/compile
  generation/cps-lift
  editor/editor

rules

  constraint-error :
    Application(qid, def*) -> (qid, $[No 'root' screen defined with no arguments. The root screen functions as the application's entry point and is required.])
    where [] := <filter(?Screen(_, QId(qid, "root"), [], _, _))> def*

  constraint-error :
    Application(_, def*) -> (qid, $[Multiple definitions of '[<pp-mobl-qid> qid]'])
    where all-names := <filter({?_#([manno*, <id>, _|_]); where([] := <filter(?WhenAnno(_))> manno*)})> def*
        ; qid := <find-duplicate> all-names

  constraint-error :
    Module(_, def*) -> (qid, $[Multiple definitions of '[<pp-mobl-qid> qid]'])
    where all-names := <filter({?_#([manno*, <id>, _|_]); where([] := <filter(?WhenAnno(_))> manno*)})> def*
        ; qid := <find-duplicate> all-names

  find-duplicate :
    [el|k] -> <find-duplicate(|el)> k

  find-duplicate :
    [el|k] -> <find-duplicate> k
    where not(<find-duplicate(|el)> k)

  find-duplicate(|el) :
   [el2|k] -> el2
   where <eq> (el, el2)

  find-duplicate(|el) :
   [el2|k] -> <find-duplicate(|el)> k
   where not(<eq> (el, el2))

rules

  constraint-warning :
    Application(qid, _) -> (qid, $[Application name does not match file path.])
    where not(<eq> ($[[<qid-to-path> qid].mobl], <CompilingFilename>))

  constraint-warning :
    Module(qid, _) -> (qid, $[Module name does not match file path.])
    where not(<eq> ($[[<qid-to-path> qid].mobl], <CompilingFilename>))

rules

  constraint-error :
    Function(_, qid, _, rt, body) -> (qid, $[Function has no return statement.])
    where not(SimpleType(QId("mobl", "void")) := rt)
    where [] := <collect(?Return(_))> body

  constraint-error :
    Function(_, qid, _, rt, body) -> (stm, $[Return statement should return expression of type [<pp-mobl-type> rt].])
    where not(SimpleType(QId("mobl", "void")) := rt)
    where [stm|_] := <collect(where(?Return(<id>); type-of; not(<type-compatible> (rt, <id>))))> body

  constraint-error :
    SyncFunction(_, qid, _, rt, body) -> (qid, $[Function has no return statement.])
    where not(SimpleType(QId("mobl", "void")) := rt)
    where [] := <collect(?Return(_))> body

  constraint-error :
    SyncFunction(_, qid, _, rt, body) -> (stm, $[Return statement should return expression of type [<pp-mobl-type> rt].])
    where not(SimpleType(QId("mobl", "void")) := rt)
    where [stm|_] := <collect(where(?Return(<id>); type-of; not(<type-compatible> (rt, <id>))))> body

  constraint-error :
    Function(_, qid, _, SimpleType(QId("mobl", "void")), body) -> (stm, $[Return statement should not return a value.])
    where [stm|_] := <collect(?Return(_))> body

  constraint-error :
    SyncFunction(_, qid, _, SimpleType(QId("mobl", "void")), body) -> (stm, $[Return statement should not return a value.])
    where [stm|_] := <collect(?Return(_))> body

rules

  constraint-error :
    t@SimpleType(_) -> (t, $[Type is not defined: [<pp-mobl-type> t]])
    where not(<lookup-type> t) // no entity for this type
        ; not(_{"ControlType"} := t)

  constraint-error :
    t@GenericType(_, _) -> (t, $[Type is not defined: [<pp-mobl-type> t]])
    where not(<lookup-type> t) // no entity for this type

  constraint-error :
    Html(tag, _, _, closeTag) -> (closeTag, $[Wrong closing tag])
    where not(<eq> (tag, closeTag))

  constraint-error :
    NamedHtml(_, tag, _, _, closeTag) -> (closeTag, $[Wrong closing tag])
    where not(<eq> (tag, closeTag))

  constraint-error :
    t@Var(qid) -> (t, $[Variable not defined: [<pp-mobl-exp> t]])
    where not("elements" := qid)
    where not(type-of)

  constraint-error :
    VarDeclInferred(x, e) -> (x, $[Not able to infer type, please declare type explicitly, e.g. var [x] : SomeType = ...])
    where not(<type-of> Var(x))

  constraint-error :
    Call(qid, e*) -> (<id>, $[Wrong set of arguments. Expected arguments are: [<filter(?FArg(<id>, _)); separate-by(!", "); concat-strings> farg*]])
    where def := <lookup-function> qid
        ; (ExternalFunction(_, _, farg*, _) := def <+ ExternalSyncFunction(_, _, farg*, _) := def)
        ; not(<resolve-fargs> (e*, <topdown(try(strip-annos))> farg*))

  constraint-error :
    Call(qid, e*) -> <fargs-type-not-compatible> (farg*, e*)
    where def := <lookup-function> qid
        ; (ExternalFunction(_, _, farg*, _) := def <+ ExternalSyncFunction(_, _, farg*, _) := def)

  constraint-error :
    t@Call(qid, e*) -> (t, $[All arguments of an entity constructors need to be named, e.g. SomeEnt(name="...", age=27).])
    where ExternalEntity(_, _, _, _) := <lookup-type> SimpleType(qid)
       <+ ExternalType(_, _, _, _) := <lookup-type> SimpleType(qid)
    where not([] := <remove-all(?NamedExp(_, _))> e*)

  constraint-error :
    t@Call(qid, e*) -> (arg, $[No such property: [arg]])
    where ExternalEntity(_, _, _, prop*) := <lookup-type> SimpleType(qid)
       <+ ExternalType(_, _, _, prop*) := <lookup-type> SimpleType(qid)
    where args := <map(?NamedExp(<id>, _))> e*
        ; arg := <fetch-elem(\ a -> a where [] := <filter(?Property(_, a, _, _))> prop* \)> args

  constraint-error :
    Call(qid, _) -> (qid, $[No such function or entity defined.])
    where not(<lookup-type> SimpleType(qid)
           <+ <lookup-function> qid
           <+ <lookup-screen> qid
           <+ <type-of> Var(qid))

  constraint-error :
    t@Choice(_, _, _) -> (t, $[Could not determine type of: [<write-to-string> t]])
    where not(type-of)

  constraint-error :
    t@Brackets(_) -> (t, $[Could not determine type of: [<write-to-string> t]])
    where not(type-of)

  constraint-error :
    ControlCall(e, e*, elem*) -> (e, $[Expression does not return a control type])
    where t := <type-of> e
    where not(<is-control-type> t)

  constraint-error :
    ControlCall(e@Var(qid@QId(_, _)), e*, _) -> (e, $[Wrong set  of arguments. Expected arguments are: [<filter(?FArg(<id>, _)); separate-by(!", "); concat-strings> farg*]])
    where ExternalControl(_, _, farg*) := <lookup-control> qid
        ; not(<resolve-fargs> (e*, <topdown(try(strip-annos))> farg*))

  constraint-error :
    ControlCall(e@Var(qid@QId(_, _)), e*, _) -> <fargs-type-not-compatible> (farg*, e*)
    where ExternalControl(_, _, farg*) := <lookup-control> qid

  constraint-error :
    ControlCall(e@Var(qid@QId(_, _)), e*, _) -> (t, $[Invalid named arguments: [<separate-by(!", "); concat-strings> invalid-args].
                                                       Valid named arguments: [<separate-by(!", "); concat-strings> named-fargs]])
    where ExternalControl(_, _, farg*) := <lookup-control> qid
        ; named-fargs := <filter(?FArgOptional(<id>, _, _)); strip-annos> farg*
        ; invalid-args := <filter(\ NamedExp(x, _) -> x where not(<fetch(?x)> named-fargs) \)> e*
        ; [t|_] := invalid-args

  constraint-error :
    t@FieldAccess(e, x) -> (t, $[Property [x] not defined])
    where <type-of> e
    where not(type-of)

  constraint-error :
    t@Call(qid{}, arg*) -> (t, $[Function not defined])
    where <map(type-of)> arg*
    where not(type-of)

  constraint-error :
    VarDecl(QId(_, _), _, e) -> (e, $[Global variables may only be initialized with simple literal expression, e.g. 10, $[$$]$, Map<String, Num>() etc.])
    where not(<is-sync> e)

  constraint-error :
    VarDeclInferred(QId(_, _), e) -> (e, $[Global variables may only be initialized with simple literal expression, e.g. 10, $[$$]$, Map<String, Num>() etc.])
    where not(<is-sync> e)

  constraint-error :
    MethodCall(e, x, e*) -> (x, $[Wrong set  of arguments. Expected arguments are: [<filter(?FArg(<id>, _)); separate-by(!", "); concat-strings> farg*]])
    where not(SimpleType(QId("mobl", "Dynamic")) := <type-of> e)
    where def := <lookup-method> (<type-of> e, x)
        ; (ExternalMethod(_, _, farg*, _) := def <+ ExternalSyncMethod(_, _, farg*, _) := def)
        ; not(<resolve-fargs> (e*, <topdown(try(strip-annos))> farg*))

  constraint-error :
    t@MethodCall(e, x, arg*) -> (t, $[No such method '[x]' defined])
    where <type-of> e
    where not(type-of)

  constraint-error :
    t@MethodCall(e, x, e*) -> <fargs-type-not-compatible> (farg*, e*)
    where ExternalMethod(_, _, farg*, _) := <lookup-function> (<type-of> e, x)

  constraint-error :
    Assignment(lhs, e) -> (lhs, $[No such variable '[lhs]' defined])
    where <is-string> lhs
    where not(<type-of> Var(lhs))

  constraint-error :
    Assignment(lv, e) -> (e, $[Cannot assign an expression of type [<pp-mobl-type> t1] to l-value of type [<pp-mobl-type> t2]])
    where t1 := <type-of> e
        ; t2 := <type-of-lvalue> lv
    where not(<type-compatible> (t2, t1))

  constraint-error :
    VarDecl(x, t, e) -> (e, $[Cannot assign an expression of type [<pp-mobl-type> t1] to variable of type [<pp-mobl-type> t]])
    where t1 := <type-of> e
    where not(<type-compatible> (t, t1))

  constraint-error :
    p@Path(part*){} -> (p, $[File not found. Note that paths are relative to the project root.])
    where not(<separate-by(|"/"); concat-strings; resolve-file> part*)

  constraint-error :
    p@AbsolutePath(part*){} -> (p, $[File not found. Note that paths are relative to the project root.])
    where not(<separate-by(|"/"); !["/"|<id>]; concat-strings; resolve-file> part*)

  constraint-error :
    Import(qid) -> (qid, $[Module not found.])
    where not(<qid-to-path; <concat-strings> [<id>, ".mobl"]; resolve-file> qid)

  constraint-error :
    Mixin(qid) -> (qid, $[Module not found.])
    where not(<qid-to-path; <concat-strings> [<id>, ".mobl"]; resolve-file> qid)

  constraint-error :
    t@SetComp(e, fltr) -> (t, $[Comprehensions can only be used with expressions of type Collection<T>.])
    where not(GenericType(QId("mobl", "Collection"), _) := <type-of> e)

rules

  constraint-error :
    VarDecl(v, _, _) -> (v, $[Variables can not have the same name as the module they are in.])
    where <eq> (<strip-annos> v, <CompilingModule>)

  constraint-error :
    VarDeclInferred(v, _) -> (v, $[Variables can not have the same name as the module they are in.])
    where <eq> (<strip-annos> v, <CompilingModule>)

  constraint-error :
    FArg(v, _) -> (v, $[Arguments can not have the same name as the module they are in.])
    where <eq> (<strip-annos> v, <CompilingModule>)

  constraint-error :
    FArgOptional(v, _, _) -> (v, $[Arguments can not have the same name as the module they are in.])
    where <eq> (<strip-annos> v, <CompilingModule>)

rules

  constraint-error :
    SetComp(e, fltr*) -> <oncetd(set-comp-error(|<type-of; ?GenericType(QId("mobl", "Collection"), [<id>])> e); ?err); !err> fltr*

  set-comp-error(|t) :
    LimitFilter(e1) -> (e1, $[Expression should be of type Num])
    where not(SimpleType(QId("mobl", "Num")) := <type-of> e1)

  set-comp-error(|t) :
    OffsetFilter(e1) -> (e1, $[Expression should be of type Num])
    where not(SimpleType(QId("mobl", "Num")) := <type-of> e1)

  set-comp-error(|t) :
    PrefetchFilter(x*) -> (x, $[Not a prefetchable property])
    where ExternalEntity(_, _, _, prop*) := <lookup-type> t
    where [x|_] := <filter({x, pt: \
                    x -> x
                    where <filter(?Property(_, x, pt, _))> prop*
                        ; not(ExternalEntity(_, _, _, _) := <lookup-type> pt) \})> x*

  set-comp-error(|t) :
    OrderAscending(prop) -> (prop, $[[<pp-mobl-type> t] has no property '[prop]'])
    where ExternalEntity(_, _, _, prop*) := <lookup-type> t
    where [] := <filter(?Property(_, prop, _, _))> prop*

  set-comp-error(|t) :
    OrderDescending(prop) -> (prop, $[[<pp-mobl-type> t] has no property '[prop]'])
    where ExternalEntity(_, _, _, prop*) := <lookup-type> t
    where [] := <filter(?Property(_, prop, _, _))> prop*

  set-comp-error(|t) :
    OrderAscending(prop) -> (prop, $[Collection cannot be ordered on property '[prop]'])
    where ExternalEntity(_, _, _, prop*) := <lookup-type> t
    where [] := <filter(?Property(_, prop, SimpleType(_), _))> prop*

  set-comp-error(|t) :
    OrderDescending(prop) -> (prop, $[Collection cannot be ordered on property '[prop]'])
    where ExternalEntity(_, _, _, prop*) := <lookup-type> t
    where [] := <filter(?Property(_, prop, SimpleType(_), _))> prop*

  set-comp-error(|t) :
    term@c#([prop, _]) -> (term, $[[<pp-mobl-type> t] has no property '[prop]'])
    where <string-starts-with(|"Set")> c
    where <is-string> prop
    where ExternalEntity(_, _, _, prop*) := <lookup-type> t
    where [] := <filter(?Property(_, prop, _, _))> prop*

  set-comp-error(|t) :
    term@c#([prop, e]) -> (e, $[Value should be of type [<pp-mobl-type> pt].])
    where <fetch-elem(?c)> ["SetEq", "SetNotEq", "SetLt", "SetLeq", "SetGt", "SetGeq"]
    where ExternalEntity(_, _, _, prop*) := <lookup-type> t
    where <filter(?Property(_, prop, pt, _))> prop*
    where not(<type-compatible> (pt, <type-of> e))

  constraint-warning:
    Entity(_, qid, _, _) -> (x, $[Entity names must start with a capital])
    where
      if <is-string> qid then
        x := qid
      else
        QId(_, x) := qid
      end
      ; not(<explode-string; Hd; is-upper> x)


rules

 fargs-type-not-compatible :
    (farg*, e*) -> (e, $[Expression should be of type [<pp-mobl-type> t] instead of [<pp-mobl-type> <type-of> e].])
    where e2* := <resolve-fargs> (e*, <topdown(try(strip-annos))> farg*)
        ; t* := <map(?FArg(_, <id>) <+ ?FArgOptional(_, <id>, _))> farg*
        ; (e, t) := <zip; fetch-elem(\ (e, t) -> <id> where <not(type-compatible)> (t, <type-of> e) \)> (e2*, t*)


/*  constraint-note:
    Module(x @ "example", _) -> (x, $[This is just an example program in the "entities" language
                                      (this note is defined in trans/check.str) ])
*/
  constraint-note = fail
