module ui-lift

imports
  include/MoBL
  rename
  type

strategies

  desugar = ui-lift

  ui-lift :
    ControlCall(e, arg*, elem*) -> <concat> [inits, [ControlCall(e, arg2*, elem*)]]
    where {| Elems
           : arg2* := <map(ui-lift-expression)> arg*
           ; inits := <bagof-Elems>
           |}
           ; not([] := inits )

  ui-lift :
    List(lvalue, t, e, elem*) -> <concat> [inits, [List(lvalue, t, e2, elem*)]]
    where {| Elems
           : e2 := <ui-lift-expression> e
           ; inits := <bagof-Elems>
           |}
           ; not([] := inits )

  ui-lift :
    ListInferred(lvalue, e, elem*) -> <concat> [inits, [ListInferred(lvalue, e2, elem*)]]
    where {| Elems
           : e2 := <ui-lift-expression> e
           ; inits := <bagof-Elems>
           |}
           ; not([] := inits )

  ui-lift-expression :
    NamedExp(x, e) -> NamedExp(x, <ui-lift-expression> e)

  ui-lift-expression =
    ?Var(_)

  ui-lift-expression =
    ?FieldAccess(_, _)

  ui-lift-expression :
    e -> Var(<rename-ref-var> (newvar, <type-of> e))
    where not(?Var(_))
        ; not(?FieldAccess(_, _))
        ; not(?NamedExp(_, _))
        ; newvar := <newname> "tmp"
        ; rules ( IsRefVar : newvar )
        ; rules ( Elems :+= VarDeclInferred(newvar, e) )