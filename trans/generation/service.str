module generation/service

imports
  libstratego-lib
  libstratego-gpp
  libstratego-aterm
  include/MoBL
  lib/editor-common
  desugar
  type
  rename
  util
  generation/emit
  generation/action
  generation/cps-lift
  generation/cps-action

rules

  definition-to-js :
    Service(_, qid, selem*) -> <emit> $[
      [<qid-to-js> qid] = {
        [body]
      };
      ]
    with body := <map(service-elem-to-js); separate-by(|",\n")> selem*

  service-elem-to-js :
    PropVal(x, e) -> $[[x]: [<expression-to-js> e]]

  service-elem-to-js :
    Resource(_, x, farg*, rt, prop*) ->
    $[[x]: function([fargs]) {
      var url = [url];
      $.ajax({
         url: url,
         dataType: [<expression-to-js> encoding],
         type: [<expression-to-js> method],
         [data-property]
         error: function(_, message, error) {
           console.error(message);
           console.error(error);
           callback(null);
         },
         success: function(data) {
            [<expression-to-js> mapper](data, callback);
         }
      });
    }
   ]
  with fargs := <filter(farg-to-js); <concat> [<id>, ["callback"]]; separate-by(!", "); concat-strings> farg*
     ; <fetch(?PropVal("uri", uri)) <+ uri := String($["/[x]"])> prop*
     ; <fetch(?PropVal("encoding", encoding)) <+ encoding := String("\"json\"")> prop*
     ; <fetch(?PropVal("mapper", mapper)) <+ mapper := Var(QId("mobl", "dummyMapper"))> prop*
     ; <fetch(?PropVal("method", method)) <+ method := String("\"GET\"")> prop*
     ; data := <map(farg-to-js-key-val); separate-by(|", ")> farg*
     ; url := <build-service-url> (uri, method, data)
     ; if not(String("\"GET\"") := method) then
         data-property := $[data: {[data]},]
       else
         data-property := ""
       end


  build-service-url :
    (uri, String("\"GET\""), data) -> $[this.root ? mobl.proxyUrl(this.root + [<expression-to-js> uri] + mobl.encodeUrlObj({[data]})) : [<expression-to-js> uri] + mobl.encodeUrlObj({[data]})]

  build-service-url :
    (uri, method, data) -> $[this.root ? mobl.proxyUrl(this.root + [<expression-to-js> uri]) : [<expression-to-js> uri]]
    where not(String("\"GET\"") := method)

  farg-to-js-key-val :
    FArg(x, _) -> $["[x]": [x]]

  farg-to-js-key-val :
    FArgOptional(x, _, _) -> $["[x]": [x]]
